/**************************************************************************************************
 GRADLE CONFIG
 *************************************************************************************************/

if ((new File(gradle.local)).exists()) {
    apply from: gradle.local
} else {
    apply from: gradle.remote
}

applicationScripts.each {
    apply from: it
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

android {
    // The following are set in the common.gradle file
    compileSdkVersion(compileVersion)
    buildToolsVersion(buildTools)

    defaultConfig {
        applicationId(groupId + submitName)
        minSdkVersion(minVersion)
        targetSdkVersion(targetVersion)
        versionCode(releaseVersionCode)
        versionName(versionCodeName)

        testApplicationId(groupId + submitName + testNameSuffix)
        testInstrumentationRunner(instrumentationRunner)

        vectorDrawables.useSupportLibrary = true
    }

    flavorDimensions "stage", "testing"

    productFlavors {
        demo {
            dimension 'stage'
        }

        master {
            dimension 'stage'
        }

        snapshot {
            dimension 'stage'
        }

        basic {
            dimension 'testing'
        }

        uitest {
            minSdkVersion(uitestMinSDK)
            dimension 'testing'
        }
    }

    signingConfigs {
        release {
            if (shouldSign) {
                storeFile file(signStoreFilePath)
                storePassword signStorePass
                keyAlias signKeyAlias
                keyPassword signKeyPass
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled(minify)
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'

            if (shouldSign) {
                signingConfig signingConfigs.release
            }
        }
        debug {
            debuggable(true)
            testCoverageEnabled(testCoverage) // Allows AndroidTest JaCoCo reports to be generated
        }
    }

    dexOptions {
        javaMaxHeapSize "2g"
    }

    packagingOptions {
        // Pick first occurrence of any files that cause conflicts, as defined in common.gradle
        packageList.each {
            pickFirst it
        }
    }

    compileOptions {
        sourceCompatibility javaVersion
        targetCompatibility javaVersion
    }
}

configurations.all {
    resolutionStrategy {
        cacheDynamicVersionsFor(0, 'seconds')
        cacheChangingModulesFor(0, 'seconds')
    }
}

dependencies {
    implementation fileTree(include: '*.jar', dir: 'libs')

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.preference:preference:1.1.0'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    implementation 'androidx.fragment:fragment:1.2.0-alpha04'

    implementation 'androidx.lifecycle:lifecycle-extensions:2.1.0'
    implementation 'androidx.lifecycle:lifecycle-common-java8:2.1.0'

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'

    implementation 'org.nanohttpd:nanohttpd:2.3.1'
    implementation 'org.nanohttpd:nanohttpd-nanolets:2.3.1'

    implementation 'com.squareup.okhttp3:okhttp:3.10.0'

    if (libraryProjectPath.exists() && gradle.useLocal) { // Local project is favoured
        implementation project(libraryProjectName)
    } else {
        demoImplementation(group: groupId, name: libraryDemoArtifactName, classifier: demoRelease,
                version: latestVersion, ext: 'aar')
        masterImplementation(group: groupId, name: libraryMasterArtifactName, classifier: masterRelease,
                version: masterArtifactVersion, ext: 'aar')
        snapshotImplementation(group: groupId, name: librarySnapshotArtifactName,
                classifier: snapshotRelease, version: latestVersion, ext: 'aar')
    }

    if (commonProjectPath.exists() && gradle.useLocal) { // Local project is favoured
        implementation project(commonProjectName)
    } else {
        demoImplementation(group: groupId, name: commonDemoArtifactName, classifier: demoRelease,
                version: latestVersion, ext: 'aar')
        masterImplementation(group: groupId, name: commonMasterArtifactName, classifier: masterRelease,
                version: masterArtifactVersion, ext: 'aar')
        snapshotImplementation(group: groupId, name: commonSnapshotArtifactName,
                classifier: snapshotRelease, version: latestVersion, ext: 'aar')
    }

    testImplementation 'androidx.arch.core:core-testing:2.0.1'
    testImplementation 'junit:junit:4.12'

    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    androidTestImplementation 'org.awaitility:awaitility:3.1.0'
}

task grantPermissionForODKApp {
    dependsOn grantServicesReadExternalStoragePermission
    dependsOn grantServicesWriteExternalStoragePermission
}

tasks.whenTaskAdded { task ->
    if(task.name.contentEquals("connectedSnapshotBasicDebugAndroidTest")) {
        task.dependsOn grantPermissionForODKApp
    }
}
